// WRITE SYSTEM CALL 
// not yet checked

// storing userSP
alias userSP R5;
userSP = SP;

// extracting system call number from user stack
alias system_call_number R6;
system_call_number = [[PTBR + 2 * (userSP - 5) / 512] * 512 + (userSP - 5) % 512];


// switching to kernel stack
alias current_PID R7;
alias process_table_entry R8;
current_PID = [SYSTEM_STATUS_TABLE + 1];
process_table_entry = PROCESS_TABLE + current_PID * 16;

[process_table_entry + 13] = SP;
SP = [process_table_entry + 11] * 512 - 1;

[process_table_entry + 9] = INT_READ;

// arguments are file Descriptor and the word to be written 

alias file_descriptor R9;
file_descriptor = [[PTBR + 2 * (userSP - 4) / 512] * 512 + (userSP - 4) % 512];
// print("file_descriptor is");
// print(file_descriptor);

alias word R10;
word = [[PTBR + 2 * (userSP - 3) / 512] * 512 + (userSP - 3) % 512];

alias return_address R11;
return_address = [PTBR + 2 * (userSP - 1) / 512] * 512 + (userSP - 1) % 512;

if(file_descriptor == -2) then
    // terminal write
    multipush(R5, R6, R7, R8, R9, R10, R11);
    R1 = TERMINAL_WRITE;
    R2 = current_PID;
    R3 = word;
    call DEVICE_MANAGER;
    multipop(R5, R6, R7, R8, R9, R10, R11);

    // return 0
    [return_address] = 0;
    [process_table_entry + 9] = 0;
    SP = userSP;
    ireturn;

endif;

// else its file write

// check if file descriptor is valid (i.e from 0 to 7)
if(file_descriptor < 0 || file_descriptor >= 8) then
    [process_table_entry + 9] = 0;
    // return -1
    [return_address] = -1;
    SP = userSP;
    ireturn;
endif;


// locate the per process resource table 

alias user_area_page_end R12;
user_area_page_end = [process_table_entry + 11] * 512 + RESOURCE_TABLE_OFFSET;

// resource identifier 
alias resource_id R13;
resource_id = [user_area_page_end + 2 * file_descriptor + 0];


if(resource_id != FILE) then
    [process_table_entry + 9] = 0;
    // return -1
    [return_address] = -1;
    SP = userSP;
    ireturn;
endif;

// get the index of open file table 
alias index_in_open_file_table R14;
index_in_open_file_table = [user_area_page_end + 2 * file_descriptor + 1];

// get the index in inode table from open file table entry
alias index_in_inode_table R15;
index_in_inode_table = [OPEN_FILE_TABLE + 4 * index_in_open_file_table + 0];

// current user is not root and current user does not own the file
// and the exclusive permission is set, return -3

if([SYSTEM_STATUS_TABLE + 0] != ROOT && [INODE_TABLE + 16 * index_in_inode_table + 3] != [SYSTEM_STATUS_TABLE + 0] && [INODE_TABLE + 16 * index_in_inode_table + 4] == EXCLUSIVE) then
    [process_table_entry + 9] = 0;
    // return -3
    [return_address] = -3;
    SP = userSP;
    ireturn;
endif;

multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15);
R1 = ACQUIRE_INODE;
R2 = index_in_inode_table;
R3 = current_PID;
call RESOURCE_MANAGER;
multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15);

if(R0 != 0) then
    // acquire inode failed, hence return -1
    [process_table_entry + 9] = 0;
    [return_address] = -1;
    SP = userSP;
    ireturn;
endif;


// get the lseek position from open file table entry 
// using registers from R4 onwards
alias lseek R4;
lseek = [OPEN_FILE_TABLE + 4 * index_in_open_file_table + 2];

if(lseek >= MAX_FILE_SIZE) then
    // release inode and return -2
    multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);
    R1 = RELEASE_INODE;
    R2 = index_in_inode_table;
    R3 = current_PID;
    call RESOURCE_MANAGER;
    multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);
    
    [process_table_entry + 9] = 0;
    // return -2
    [return_address] = -2;
    SP = userSP;
    ireturn;
endif;

if((lseek % 512) == 0 && (lseek == [INODE_TABLE + 16 * index_in_inode_table + 2])) then
    // new block to be allocated 
    multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);
    R1 = GET_FREE_BLOCK;
    // no arguments
    call MEMORY_MANAGER;
    multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);

    if(R0 == -1) then
        // no free disk block 
        // release inode and return -2
        multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);
        R1 = RELEASE_INODE;
        R2 = index_in_inode_table;
        R3 = current_PID;
        call RESOURCE_MANAGER;
        multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4);

        [process_table_entry + 9] = 0;
        // return -2
        [return_address] = -2;
        SP = userSP;
        ireturn;
    endif;

    // set the new disk block in the corresponding inode table entry
    [INODE_TABLE + 16 * index_in_inode_table + 8 + (lseek / 512)] = R0;

endif;

// Find the disk block number and the 
// position in the block from which input is to be written.


// Get the block index from lseek position.   /* block index = lseek / block size (512) */
// Get the disk block number corresponding to the block index from the Inode Table .
// Get the offset value from lseek position.   /* offset = lseek % the block size (512) */

multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4, R3);
R1 = BUFFERED_WRITE;
R4 = word;
// since lseek in R4 is changed to word,
// because R4 is an argument to BUFFERED_WRITE
R3 = [OPEN_FILE_TABLE + 4 * index_in_open_file_table + 2];
// R3 is still not the correct argument, we have to take modulo 512
// but R3 helps in defining R2
R2 = [INODE_TABLE + 16 * index_in_inode_table + 8 + (R3 / 512)];
R3 = R3 % 512;
call FILE_MANAGER;
multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4, R3);

// if lseek equals file size in inode table 
if(lseek == [INODE_TABLE + 16 * index_in_inode_table + 2]) then
    // increment file size in the inode table entry and also in the memory copy of the root file.
    [INODE_TABLE + 16 * index_in_inode_table + 2] = [INODE_TABLE + 16 * index_in_inode_table + 2] + 1;
    [ROOT_FILE + 8 * index_in_inode_table + 1] = [ROOT_FILE + 8 * index_in_inode_table + 1] + 1;
endif;

// increment lseek in the open file table entry
[OPEN_FILE_TABLE + 4 * index_in_open_file_table + 2] = [OPEN_FILE_TABLE + 4 * index_in_open_file_table + 2] + 1;

// release the lock by calling RELEASE_INODE
multipush(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4, R3);
R1 = RELEASE_INODE;
R2 = index_in_inode_table;
R3 = current_PID;
call RESOURCE_MANAGER;
multipop(R5, R6, R7, R8, R9, R10, R11, R12, R13, R14, R15, R4, R3);

[process_table_entry + 9] = 0;

// return 0
[return_address] = 0;

// switch back to user stack 
SP = userSP;
ireturn;



